import org.jooq.util.GenerationTool
import org.jooq.util.jaxb.Configuration
import org.jooq.util.jaxb.CustomType
import org.jooq.util.jaxb.Database
import org.jooq.util.jaxb.ForcedType
import org.jooq.util.jaxb.Generate
import org.jooq.util.jaxb.Generator
import org.jooq.util.jaxb.Jdbc
import org.jooq.util.jaxb.Schema
import org.jooq.util.jaxb.Target

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'spring-boot'

mainClassName = 'io.github.picodotdev.blogbitix.multidatabase.Main'

buildscript {
    repositories {
        mavenCentral()        
    }
   
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.7.RELEASE")
        classpath 'org.jooq:jooq-codegen:3.7.0'
        classpath 'com.h2database:h2:1.4.189'
        classpath 'org.postgresql:postgresql:9.4-1203-jdbc41'
        classpath 'mysql:mysql-connector-java:5.1.36'
    }
}

repositories {
    mavenCentral()
}

dependencies {
	// Spring
	compile 'org.springframework:spring-context:4.2.1.RELEASE'	
	compile 'org.springframework:spring-jdbc:4.2.1.RELEASE'
	compile 'org.springframework:spring-tx:4.2.1.RELEASE'
	compile('org.springframework.boot:spring-boot-starter:1.2.7.RELEASE') { exclude(group: 'ch.qos.logback') }
    compile('org.hibernate:hibernate-validator:5.2.2.Final')

	// Database	
    compile 'org.jooq:jooq:3.7.0'
    compile 'org.jooq:jooq-meta:3.7.0'
    compile 'commons-dbcp:commons-dbcp:1.4'
	runtime 'com.h2database:h2:1.4.189'

	// Logging
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.3'
    compile 'org.apache.logging.log4j:log4j-api:2.3'
    compile 'org.apache.logging.log4j:log4j-core:2.3'

	// Misc
  	compile 'org.apache.commons:commons-lang3:3.4'
}

task generateModels << {
    Configuration configuration = new Configuration()
        .withJdbc(new Jdbc()
            .withDriver('org.h2.Driver')
            .withUrl('jdbc:h2:./misc/database/app')
            .withUser('sa')
            .withPassword('sa'))
        .withGenerator(new Generator()
            .withGenerate(new Generate()
                .withInterfaces(true)
                .withPojos(true)
                .withRelations(true)
                .withValidationAnnotations(true))
            .withName('org.jooq.util.DefaultGenerator')
            .withDatabase(new Database()
                .withCustomTypes([
                    new CustomType()
                        .withName('java.time.LocalDateTime')
                        .withConverter('io.github.picodotdev.blogbitix.multidatabase.misc.TimestampConverter')
                ])
                .withForcedTypes([
                    new ForcedType()
                        .withName('java.time.LocalDateTime')
                        .withTypes('TIMESTAMP')
                ])
                .withName('org.jooq.util.h2.H2Database')
                .withIncludes('.*')
                .withExcludes('')
                .withSchemata([
                    new Schema().withInputSchema('INVENTORY'),
                    new Schema().withInputSchema('PURCHASES')
                ]))
            .withTarget(new Target()
                .withPackageName('io.github.picodotdev.blogbitix.multidatabase.jooq')
                .withDirectory('src/main/java')))

    GenerationTool.main(configuration)
}

task updateDatabase() << {
	tasks.updateDatabaseInventory.execute()
	tasks.updateDatabasePurchases.execute()
}

task updateDatabaseInventory(type:Exec) {
	commandLine "misc/liquibase-3.4.1/liquibase", '--classpath=misc/libs/h2-1.4.189.jar', '--driver=org.h2.Driver',
		'--changeLogFile=misc/database/inventory-changelog.xml', '--url=jdbc:h2:./misc/database/app', '--username=sa', '--password=sa', 'update'
}

task updateDatabasePurchases(type:Exec) {
	commandLine "misc/liquibase-3.4.1/liquibase", '--classpath=misc/libs/h2-1.4.189.jar', '--driver=org.h2.Driver',
		'--changeLogFile=misc/database/purchases-changelog.xml', '--url=jdbc:h2:./misc/database/app', '--username=sa', '--password=sa', 'update'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
