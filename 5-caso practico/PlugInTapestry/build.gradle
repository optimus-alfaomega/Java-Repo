plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'project-report'
    id 'pmd'
    id 'nu.studer.jooq' version '5.2.1'
    id 'org.liquibase.gradle' version '2.0.4'
}

application {
    description = 'PlugInTapestry application'
    group = 'io.github.picodotdev.plugintapestry'
    version = '1.8'
    mainClass = 'io.github.picodotdev.plugintapestry.Main'

    sourceCompatibility = '11'
    targetCompatibility = '11'
}

ext {
    versions = [
        tapestry:             '5.6.2',
        tapestrySecurity:     '0.7.1',
        tapestryTestify:      '1.0.4',
        tapestryXpath:        '1.0.1',
        commons_dbcp:         '1.4',
        hibernate:            '5.1.16.Final',
        hibernateValidator:   '6.2.0.Final',
        geb:                  '4.0',
        groovy:               '3.0.7',
        guava:                '27.0.1-jre',
        h2:                   '1.4.200',
        htmlunitDriver:       '2.33.3',
        htmlunitCsspaser:     '1.2.0',
        jackson:              '2.9.6',
        javaee:               '8.0',
        json:                 '1.1.2',
        jooq:                 '3.14.8',
        junit:                '4.12',
        liquibase:            '4.3.2',
        liquibaseGroovyDsl:   '3.0.0',
        log4j2:               '2.11.1',
        mockito:              '2.23.4',
        selenium:             '3.141.59',
        shiro:                '1.3.2',
        spock:                '2.0-M5-groovy-3.0',
        spring:               '5.0.11.RELEASE',
        springBoot:           '2.1.4.RELEASE',
        jbossVfs:             '3.2.15.Final',
        jbossLogging:         '3.3.2.Final',
        webjars: [
            requirejs:        '2.3.5',
            jquery:           '3.3.1-1',
            underscore:       '1.9.1',
            bootstrapSelect:  '1.13.8'
        ],
        yasson:               '1.0.1'
    ]
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repository.apache.org/content/repositories/staging/'
    }
}

// This simulates Maven's 'provided' scope, until it is officially supported by Gradle
// See http://jira.codehaus.org/browse/GRADLE-784
configurations {
    appJavadoc
}

configurations.all {
    exclude(group: 'org.springframework.boot', module: 'spring-boot-starter-logging')
    resolutionStrategy {
        force "org.hibernate:hibernate-core:$versions.hibernate"
        force "org.apache.shiro:shiro-all:$versions.shiro"
        force "org.codehaus.groovy:groovy:$versions.groovy"
    }
}

sourceSets {
    jooq {
        java {
            srcDir 'src/main/jooq'
        }
    }
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:$versions.springBoot")

    // Tapestry
    implementation("org.apache.tapestry:tapestry-core:$versions.tapestry")
    implementation("org.apache.tapestry:tapestry-hibernate:$versions.tapestry")
    implementation("org.apache.tapestry:tapestry-beanvalidator:$versions.tapestry")

    // Compresión automática de javascript y css en el modo producción
    implementation("org.apache.tapestry:tapestry-webresources:$versions.tapestry")
    appJavadoc("org.apache.tapestry:tapestry-javadoc:$versions.tapestry")

    // Spring
    implementation("org.apache.tapestry:tapestry-spring:$versions.tapestry")
    implementation("org.springframework:spring-jdbc:$versions.spring")
    implementation("org.springframework:spring-orm:$versions.spring")
    implementation("org.springframework:spring-tx:$versions.spring")

	// Spring Boot
    implementation("org.springframework.boot:spring-boot-starter")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-log4j2")
    implementation("org.springframework.boot:spring-boot-autoconfigure")
    implementation("org.springframework.boot:spring-boot-starter-tomcat")

    // Persistencia
    implementation("org.jooq:jooq:$versions.jooq")
    implementation("commons-dbcp:commons-dbcp:$versions.commons_dbcp")
    implementation("com.h2database:h2:$versions.h2")
    
    // Seguridad
    implementation("org.tynamo:tapestry-security:$versions.tapestrySecurity")

    // Otras
    implementation("javax:javaee-api:$versions.javaee")
    implementation("com.google.guava:guava:$versions.guava")
    implementation("org.jboss:jboss-vfs:$versions.jbossVfs")
    implementation("org.hibernate:hibernate-validator:$versions.hibernateValidator")

    // Pruebas unitarias
    testImplementation("org.apache.tapestry:tapestry-test:$versions.tapestry") { exclude(group: 'org.testng'); exclude(group: 'org.seleniumhq.selenium') }
    testImplementation("net.sourceforge.tapestrytestify:tapestry-testify:$versions.tapestryTestify")
    testImplementation("net.sourceforge.tapestryxpath:tapestry-xpath:$versions.tapestryXpath")
    testImplementation("junit:junit:$versions.junit")
    testImplementation("org.mockito:mockito-core:$versions.mockito")
    testImplementation("org.spockframework:spock-core:$versions.spock")
    testImplementation("org.spockframework:spock-spring:$versions.spock")
 
    // Pruebas de integración    
    testImplementation("org.gebish:geb-spock:$versions.geb")
    testImplementation("org.gebish:geb-junit4:$versions.geb")
    testImplementation("org.seleniumhq.selenium:selenium-support:${versions.selenium}")
    testImplementation("org.seleniumhq.selenium:htmlunit-driver:${versions.htmlunitDriver}")
    testImplementation("org.springframework.boot:spring-boot-starter-test")

    // Webjars
    runtimeOnly("org.webjars:requirejs:$versions.webjars.requirejs")
    runtimeOnly("org.webjars:jquery:$versions.webjars.jquery")
    runtimeOnly("org.webjars.bower:underscore:$versions.webjars.underscore")
    runtimeOnly("org.webjars:bootstrap-select:$versions.webjars.bootstrapSelect")

    runtimeOnly("org.jboss.logging:jboss-logging:$versions.jbossLogging")
    runtimeOnly("org.eclipse:yasson:$versions.yasson")
    runtimeOnly("org.glassfish:javax.json:$versions.json")
    runtimeOnly("com.fasterxml.jackson.core:jackson-databind:$versions.jackson")
    runtimeOnly("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$versions.jackson")

    jooqGenerator("com.h2database:h2:$versions.h2")
    liquibaseRuntime("org.liquibase:liquibase-core:$versions.liquibase")
    liquibaseRuntime("org.liquibase:liquibase-groovy-dsl:$versions.liquibaseGroovyDsl")
    liquibaseRuntime("com.h2database:h2:$versions.h2")
}

pmd {
    ruleSets = ["bestpractices"]
    ruleSetFiles = files("misc/ruleset.xml")
}

test {
    // Excluir de los teses unitarios los teses de integración
    exclude '**/geb/*Spec.*'
}

task integrationTest(type: Test) {
    group = 'Verification'
    description = 'Runs the integration/functional tests.'
    systemProperty 'geb.driver', 'htmlunit'

    // Incluir los teses de integración
    include '**/geb/*Spec.*'
}

task appJavadoc(type: Javadoc) {
    classpath = sourceSets.main.compileClasspath
    source = sourceSets.main.allJava
    destinationDir = reporting.file('appJavadoc')
    options.tagletPath = configurations.appJavadoc.files.asType(List)
    options.taglets = ['org.apache.tapestry5.javadoc.TapestryDocTaglet']
    
    doLast {
        copy {
            from sourceSets.main.java.srcDirs
            into appJavadoc.destinationDir
            exclude '**/*.java'
            exclude '**/*.xdoc'
            exclude '**/package.html'
        }

        copy {
            from file('src/javadoc/images')
            into appJavadoc.destinationDir
        }
    }
}

jooq {
    version = '3.14.8'
    edition.set(nu.studer.gradle.jooq.JooqEdition.OSS)
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.h2.Driver'
                    url = 'jdbc:h2:./misc/database/app'
                    user = 'sa'
                    password = 'sa'
                }
                generator {
                    name = "org.jooq.codegen.DefaultGenerator"
                    database {
                        name = 'org.jooq.meta.h2.H2Database'
                        inputSchema = 'PLUGINTAPESTRY'
                    }
                    generate {
                        interfaces = true
                        pojos = true
                        relations = true
                        validationAnnotations = true
                    }
                    target {
                        packageName = 'io.github.picodotdev.plugintapestry.entities.jooq'
                        directory = 'src/main/jooq'
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

liquibase {
    activities { 
        main {
            changeLogFile 'misc/database/changelog.xml'
            url 'jdbc:h2:./misc/database/app'
            username 'sa'
            password 'sa'
        }
    }
    runList = 'main'
}

tasks.named('generateJooq').configure { allInputsDeclared = true }
